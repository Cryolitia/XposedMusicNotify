apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "cn.nexus6p.QQMusicNotify"
        minSdkVersion 23
        //noinspection OldTargetApi,ExpiredTargetSdkVersion
        targetSdkVersion 29
        vectorDrawables.useSupportLibrary = true
    }

    repositories {
        maven { url 'https://jitpack.io' }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        release {
            if (rootProject.file("keystore.properties").exists()) {
                def keystorePropertiesFile = rootProject.file("keystore.properties")
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                storeFile file('nexus6pcn.jks')
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    flavorDimensions "type"
    productFlavors {
        play {
            dimension "type"
        }
        apk {
            dimension "type"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    viewBinding {
        enabled = true
    }

    applicationVariants.all { variant ->
        def buildType = variant.buildType.name
        def variantName = variant.productFlavors[0].name
        if (buildType == "release" && variantName == "apk") {
            variant.packageApplicationProvider.get().outputDirectory = new File(project.rootDir.absolutePath + "/app/release")
            variant.outputs.all { output ->
                output.outputFileName = "XposedMusicNotify-" + variant.versionName + "-release.apk"
            }
        }
    }

}

task buildAllRelease {
    group = 'build'
    dependsOn 'bundlePlayRelease'
    dependsOn 'assembleApkRelease'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar'])
    compileOnly 'de.robv.android.xposed:api:82'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'com.github.topjohnwu.libsu:core:2.5.0'
    implementation 'de.psdev.licensesdialog:licensesdialog:2.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.google.android.material:material:1.2.0-alpha06'
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.1.0"
}
repositories {
    mavenCentral()
}
